apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
        targetCompatibility = rootProject.ext.targetCompatibilityVersion
    }

    defaultConfig {
        applicationId "org.adblockplus.adblockplussbrowser"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 12
        versionName '1.1.2'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation'
            }
        }
    }

    afterEvaluate { project ->
        project.tasks.generateReleaseResources << {
            updateBundledLists()
            println('Bundled lists updated at ' + new Date().format('dd MMM yyyy, HH:mm:ss zzz'))
        }
    }
}

def updateBundledLists() {
    new URL(rootProject.ext.easyListUpdateUrl).withInputStream {
        i -> new File(rootProject.ext.easyListFilePath).withOutputStream { it << i }
    }
    new URL(rootProject.ext.exceptionRulesUpdateUrl).withInputStream {
        i -> new File(rootProject.ext.exceptionRulesFilePath).withOutputStream { it << i }
    }
}

dependencies {
    testCompile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
    compile "com.android.support:support-v4:$rootProject.ext.androidSupportLibraryVersion"
    compile (group: 'commons-validator', name: 'commons-validator', version: '+') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}