apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: 'translations.gradle'


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
        targetCompatibility = rootProject.ext.targetCompatibilityVersion
    }

    defaultConfig {
        applicationId "org.adblockplus.adblockplussbrowser"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 17
        versionName '1.2.0'
        buildConfigField "String", "FLAVOR_REGION_WORLD", '"world"'
        buildConfigField "String", "FLAVOR_REGION_CHINA", '"china"'
        buildConfigField "String", "FLAVOR_PRODUCT_ABP", '"abp"'
        buildConfigField "String", "FLAVOR_PRODUCT_CRYSTAL", '"crystal"'
    }
    
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation'
            }
        }
    }

    flavorDimensions "region", "product"

    productFlavors {
        world {
            dimension "region"
        }

        china {
            dimension "region"
            applicationIdSuffix ".cn"
        }

        abp {
            dimension "product"
        }

        crystal {
            dimension "product"
            applicationId "co.crystalapp.crystal"
        }

        yandex {
            dimension "product"
            applicationId "org.adblockplus.adblockplusyandex"
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "app_version",
                "${defaultConfig.versionName} - ${variant.productFlavors[0].name}"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    afterEvaluate { project ->

        project.tasks.all { task ->
            if (task.name.matches("generate.*Release.*Resources")) {
                task.doLast {
                    updateBundledLists(task.name)
                    println('Bundled lists updated at ' + new Date().format('dd MMM yyyy, HH:mm:ss zzz'))
                }
            }
        }
    }
}

def updateBundledLists(taskName) {
    def easylistUrl = taskName.contains("China") ?
            rootProject.ext.easyListChinaUpdateUrl : rootProject.ext.easyListDefaultUpdateUrl
    new URL(easylistUrl).withInputStream {
        i -> new File(rootProject.ext.easyListFilePath).withOutputStream { it << i }
    }
    new URL(rootProject.ext.exceptionRulesUpdateUrl).withInputStream {
        i -> new File(rootProject.ext.exceptionRulesFilePath).withOutputStream { it << i }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.18.0'
    testImplementation 'org.robolectric:robolectric:4.0-beta-1'
    implementation "com.android.support:support-v4:$rootProject.ext.androidSupportLibraryVersion"
    implementation (group: 'commons-validator', name: 'commons-validator', version: '1.6') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}